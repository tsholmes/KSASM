.import libutils
.import libsystem

.ifndef libterm { .macro libterm
.ns term.

.region data 2048

:p24 ptr: system.terminal_data 0

.macro line(n) (system.terminal_data+32*n)

# _ -> _
# clear the terminal and reset cursor to top-left
clear:
  st term.ptr system.terminal_data:p24
  st:u64*8 .term.line(0) 0
  st:u64*8 .term.line(2) 0
  st:u64*8 .term.line(4) 0
  st:u64*8 .term.line(6) 0
  st:u64*8 .term.line(8) 0
  st:u64*8 .term.line(10) 0
  st:u64*8 .term.line(12) 0
  st:u64*8 .term.line(14) 0
  ret

# _ -> _
# shift all lines up
# shift cursor up and to the beginning of line
up:
  ld:u64*8 .term.line(1)
  st:u64*8 .term.line(0)
  ld:u64*8 .term.line(3)
  st:u64*8 .term.line(2)
  ld:u64*8 .term.line(5)
  st:u64*8 .term.line(4)
  ld:u64*8 .term.line(7)
  st:u64*8 .term.line(6)
  ld:u64*8 .term.line(9)
  st:u64*8 .term.line(8)
  ld:u64*8 .term.line(11)
  st:u64*8 .term.line(10)
  ld:u64*8 .term.line(13)
  st:u64*8 .term.line(12)
  ld:u64*8 .term.line(15)
  st:u64*8 .term.line(14)
  st:u64*4 .term.line(15) 0
  ld:p24 term.ptr
  sub:p24 system.terminal_data
  and:p24 (~31) # line start
  sub:p24 32 # line up
  max:p24 0 # don't move cursor before start
  add:p24 system.terminal_data
  st:p24 term.ptr
  ret

# _ -> _
# shift up if cursor is at end
_tryup:
  ld:p24 term.ptr
  bge:p24 term.up .term.line(16)
  ret

# _ -> _
# shift up if cursor on last line
# move cursor to start of next line
nl:
  ld:p24 term.ptr
  sub:p24 system.terminal_data
  and:p24 (~31)
  add:p24 (system.terminal_data+32)
  st:p24 term.ptr
  jump term._tryup

# str:s48 -> _
# str.ptr @ f+0
# str.len @ f+3
# print the string to the current terminal cursor
# shift up lines as needed
print:
  ldf:p24 (+3) # str.len
  ld:p24 term.ptr
  add:p24
  min:p24 .term.line(16)
  ld:p24 term.ptr
  sub:p24 # str.len capped at terminal end
  dup:p24 4
  ldf:p24 (+0) # str.ptr
  ld:p24 term.ptr
  call memcpy
  ld:p24 term.ptr
  add:p24
  st:p24 term.ptr
  call term._tryup
  mul:p24 (1,-1)
  ldf:s48 (+0)
  add:p24*2
  stf:s48 (+0)
  ldf:p24 (+3)
  bpos:p24 term.print
  adjf (+6)
  ret

.endns
}